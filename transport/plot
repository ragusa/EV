#!/usr/bin/env perl
# reads the input file to determine which problem was
# just run and then plots the results of that run
# with gnuplot and then views the resulting plot with evince

use strict;

# option to crop pdf to remove excess whitespace
my $crop_file = 1;

# problem ID variables
my $problem_ID;                           # value
my $problem_ID_found = 0;                 # flag for being found
my $problem_ID_search = "set Problem ID"; # search string

# time integrator variables
my $time_integrator;                           # value
my $time_integrator_found = 0;                 # flag for being found
my $time_integrator_search = "set Time integrator option"; # search string

# read input file to determine the test problem ID and time integrator
my $input_file = "input";
open(IN, '<', $input_file) or die $!;
my $input_line; # input file line
my @input_line; # array of fields of input line
while ($input_line = <IN>) {
   chomp($input_line);
   @input_line = split /\s+=\s+/, $input_line;
   if ($input_line[0] eq $problem_ID_search) {
      $problem_ID = $input_line[1];
      $problem_ID_found = 1;
   }
   if ($input_line[0] eq $time_integrator_search) {
      $time_integrator = $input_line[1];
      $time_integrator_found = 1;
   }
}

# check that both problem ID and time integrator were found
if (not $problem_ID_found) {
   die "Problem ID could not be determined because search string
        \"$problem_ID_search\" could not be found in input file.\n"
}
if (not $time_integrator_found) {
   die "Time integrator could not be determined because search string
        \"$time_integrator_search\" could not be found in input file.\n"
}

# determine time integrator string based on time integrator option integer
my $time_integrator_string;
if ($time_integrator == 1) {
   $time_integrator_string = "FE";
} elsif ($time_integrator == 3) {
   $time_integrator_string = "SSPRK33";
} else {
   die "Invalid time integrator option.\n";
}

# go to plotscripts directory
chdir "plotscripts";

# call gnuplot
system("gnuplot -e \"problem_ID='$problem_ID'; timeintegrator='$time_integrator_string'\" solutions.gp");
system("sleep 1");

# name of resulting plot file
my $output_file = "../plots/solutions_$problem_ID"."_$time_integrator_string.pdf";

# crop pdf if specified
if ($crop_file) {
   system("pdfcrop $output_file $output_file");
}

# view output
system("evince $output_file");
